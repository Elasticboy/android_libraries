apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

def moduleGroupId = 'com.cyrillrx.android'
def moduleName = 'core'
def moduleVersion = '0.4.1'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName moduleVersion
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Google Support libraries
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    // External libraries
    implementation 'com.google.code.gson:gson:2.8.2'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}


File confFile = rootProject.file('../bintray/cyrillrx.properties')
Properties publishConf = new Properties()
publishConf.load(new FileInputStream(confFile))

publish {
    bintrayUser = publishConf['user']
    bintrayKey = publishConf['key']

    userOrg = 'cyrillrx'
    groupId = moduleGroupId
    artifactId = moduleName
    publishVersion = moduleVersion
    desc = 'A basic library for Android containing UI components, and miscellaneous utils.'
    website = 'https://github.com/cyrillrx/android_libraries'
}